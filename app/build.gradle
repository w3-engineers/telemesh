plugins {
    id 'com.android.application'
    id 'io.left.rightmesh.rightmesh-plugin' version '1.8.4'
    id 'com.github.kt3k.coveralls'
    id 'com.google.protobuf'
}
//MMH apply from: 'lint-checks.gradle'

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.w3engineers.unicef.telemesh"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    lintOptions {

        htmlReport true
        htmlOutput file("../docs/lint_reports/lint-report.html")

        abortOnError true
//        absolutePaths false causing error
        warningsAsErrors true
        checkAllWarnings true

        disable 'InvalidPackage'

//        check getProject().ext.lintchecks
//        lintConfig file("lint.xml")
//        baseline file("lint-baseline.xml")
    }
    dataBinding {
        enabled = true
    }
}

//MMH apply from: 'lint-checks.gradle'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //Support
    implementation deps.support.app_compat
    implementation deps.support.design
    implementation deps.constraint_layout
    implementation deps.support.recyclerview
    implementation deps.support.cardview
    implementation deps.support.animated_vector_drawable
    implementation deps.support.v4
    implementation deps.support.media_compact

    //lifecycle
    implementation deps.lifecycle.runtime
    implementation deps.lifecycle.extensions
    annotationProcessor deps.lifecycle.compiler

    //LiveDataReactStream
    implementation deps.livedata_reactive

    //room with Rx
    implementation deps.room.runtime
    implementation deps.room.rxjava2
    annotationProcessor deps.room.compiler

    // RxJava
    implementation deps.rx_android
    implementation deps.rxjava2

    //Paging
    implementation deps.paging.runtime
    //

    //Circular Image
    implementation deps.circularimage.runtime

    //Retrofit
    implementation deps.retrofit.runtime
    implementation deps.retrofit.gson
    implementation deps.okhttp_logging_interceptor
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

    // Dependencies for local unit tests
    testImplementation deps.junit
    testImplementation deps.hamcrest
    testImplementation deps.arch_core.testing
    testImplementation deps.mockito.all
    testImplementation deps.truth
    androidTestImplementation deps.atsl.rules
    androidTestImplementation deps.atsl.runner
    androidTestImplementation deps.espresso.core
    androidTestImplementation deps.espresso.contrib
    testImplementation deps.espresso.intents

    androidTestImplementation deps.room.testing
    androidTestImplementation deps.arch_core.testing
    androidTestImplementation deps.retrofit.mock
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'

    implementation 'com.jakewharton.rxbinding2:rxbinding:2.2.0'

    //RightMesh
    implementation deps.right_mesh

    api deps.framework

    // Glide
    implementation deps.glide
    implementation deps.multidex

    //this bottom bar library is used as experiment.
    //Too much dependency on this library must be avoided
    implementation 'com.github.ittianyu:BottomNavigationViewEx:1.2.4'
    // shimmer effect
    implementation 'com.facebook.shimmer:shimmer:0.4.0'
    implementation 'com.orhanobut:dialogplus:1.11@aar'
    implementation 'com.google.zxing:core:3.3.3'

    api project(':appshare')

    /*androidTestImplementation group: 'com.github.javafaker', name: 'javafaker', version: '0.16'*/

//    api project(':viper')
}

coveralls {
    jacocoReportPath = "${buildDir}/reports/coverage/debug/report.xml"
}

tasks.coveralls {
    dependsOn 'connectedAndroidTest'
    onlyIf { System.env.'CI' }
}

// protobuf-start
protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite {}
            }
        }
    }
}
// protobuf-end

rightmesh {
    appKey = project.hasProperty('telemesh') ? project.getProperty('telemesh') : System.getenv('telemesh')

    username = System.getenv('rightmesh_build_username')

    if (username.equals("") || username.equals(null)) {
        username = rightmesh_build_username
    }

    password = System.getenv('rightmesh_build_password')

    if (password.equals("") || password.equals(null)) {
        password = rightmesh_build_password
    }
}

preBuild.dependsOn("rightmesh")

repositories {
    maven {
        url "http://artifactory.rightmesh.io/artifactory/maven"
        credentials {
            username = System.getenv('rightmesh_build_username')

            if (username.equals("") || username.equals(null)) {
                username = rightmesh_build_username
            }

            password = System.getenv('rightmesh_build_password')

            if (password.equals("") || password.equals(null)) {
                password = rightmesh_build_password
            }
        }
    }
    maven{
        url "https://dl.bintray.com/ethereum/maven/"
    }
    mavenCentral()
    jcenter()

    maven {
        url "https://jitpack.io"
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}